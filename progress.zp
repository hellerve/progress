(define-struct progressbar (len step start fill free))

(define-syntax with-progress-bar
  (syntax-rules (:as :to :step)
    ((macro-expand :as name :to len :step step body ...)
      (let ((name (make-progress-bar len step)))
        (begin
          body ...)))
    ((macro-expand :as name :to len body ...)
      (let ((name (make-progress-bar len)))
        (begin
          body ...)))))

(defkeywords (make-progress-bar len) (:step :default (/ len 10)
                                      :fill :default "#"
                                      :free :default " ")
  "create a new progress bar holding <par>len</par> elements.

   params:
    - len: an integer denoting the length of the progress bar
   complexity: O(1)
   returns: a new progress-bar instance"
  (progressbar:make-progressbar len step (car (unix-timestamp)) fill free))

(define update-progress-bar
  (let* ((string-two (lambda (s) (++ (if (= 0 (/ s 10)) "0" "") (->string s))))
         (fmt-time-delta (lambda (total-seconds)
           (let* ((seconds (mod total-seconds 60))
                  (total-minutes (/ total-seconds 60))
                  (minutes (mod total-minutes 60))
                  (hours (/ total-minutes 60)))
           (++ (string-two hours) ":" (string-two minutes) ":" (string-two seconds))))))

    (lambda (bar progress)
      "update a progress bar <par>bar</par> with <par>progress</par> and write
       the new status to stdout.

       params:
        - bar: a progress bar generated with <fun>make-progress-bar</fun>
        - progress: an integer denoting the progress
       complexity: O(1)
       returns: nil"
      (let* ((len     (progressbar:get-len bar))
             (step    (progressbar:get-step bar))
             (fill    (progressbar:get-fill bar))
             (free    (progressbar:get-free bar))
             (percent (floor (* (/. progress len) 100)))
             (time-delta (- (car (unix-timestamp)) (progressbar:get-start bar))))
        (display (++ "\r["
                     (string:replicate (floor (/ progress step)) fill)
                     (string:replicate (ceil (/. (- len progress) step)) free)
                     "] " (->string percent) "% (" (->string progress) "/" (->string len) ") "
                     "Elapsed Time: "
                     (fmt-time-delta time-delta)
                     " ETA: "
                     (fmt-time-delta (floor (* time-delta (/. (- 100 percent) (max percent 1))))))
               :flush)))))
